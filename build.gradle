plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

version = '1.2.0'
group = 'org.teacon'
archivesBaseName = 'Gongdaobei-Universal-1.20'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

configurations.register('universalClasspath') {
    canBeConsumed = false
    canBeResolved = true
}

repositories {
    mavenCentral()
}

dependencies {
    universalClasspath project(path: ':util', configuration: 'utilJar')
    universalClasspath project(path: ':forge', configuration: 'forgeJar')
    universalClasspath project(path: ':bungee', configuration: 'bungeeJar')
    shadow 'com.vdurmont:semver4j:3.1.0'
    shadow 'com.moandjiezana.toml:toml4j:0.7.2'
    shadow 'io.lettuce:lettuce-core:6.2.4.RELEASE'
    shadow 'org.apache.commons:commons-text:1.10.0'
    shadow 'io.prometheus:simpleclient_httpserver:0.16.0'
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.universalClasspath, project.configurations.shadow]
    dependencies {
        it.exclude it.dependency('io.netty:.*')
        it.exclude it.dependency('com.google.code.gson:.*')
    }
    from('LICENSE') {
        into 'META-INF'
    }
    manifest {
        attributes([
                "Specification-Title"     : "Gongdaobei",
                "Specification-Vendor"    : "TeaConMC",
                "Specification-Version"   : "1",
                "Implementation-Title"    : rootProject.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "TeaConMC",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    relocate 'reactor', 'org.teacon.gongdaobei.reactor'
    relocate 'io.lettuce', 'org.teacon.gongdaobei.lettuce'
    relocate 'io.prometheus', 'org.teacon.gongdaobei.prometheus'
    relocate 'org.apache.commons', 'org.teacon.gongdaobei.commons'
    relocate 'com.moandjiezana.toml', 'org.teacon.gongdaobei.toml'
    relocate 'com.vdurmont.semver4j', 'org.teacon.gongdaobei.semver4j'
    relocate 'org.reactivestreams', 'org.teacon.gongdaobei.reactivestreams'
}

jar.enabled = false

artifacts {
    archives shadowJar
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = "Gongdaobei-Universal-1.20"
            artifact shadowJar
            pom {
                name = "Gongdaobei Universal 1.20"
                description = "Gongdaobei: Service discovery and registration"
                licenses {
                    license {
                        name = "LGPL-3.0"
                        url = "https://www.gnu.org/licenses/lgpl-3.0.html"
                    }
                }
                developers {
                    developer {
                        id = "TeaConMC"
                        name = "TeaConMC"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "TeaConOSS"
            url = "s3://maven/"
            credentials(AwsCredentials) {
                accessKey = System.env.TEACON_ARCHIVE_ACCESS_KEY
                secretKey = System.env.TEACON_ARCHIVE_ACCESS_SECRET
            }
        }
        maven {
            name = "TeaConGitea"
            url = "https://git.teacon.cn/api/packages/teaconmc/maven"
            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token " + System.getenv("GITEA_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
